; NSIS description file for kingpinq3 installer
;
; EDIT WITH CARE!
;
; This file is used to automatically build the installers in the
; openSUSE build service, don't break this!
;
; you have to copy SDL.dll and libcurl-4.dll here manually

!include "MUI2.nsh"

; The name of the installer
Name "kingpinq3"

; The file to write
OutFile "kingpinq3-XXXVERSIONXXX-XXXRELEASEXXX.x86.exe"

; The default installation directory
InstallDir $PROGRAMFILES\kingpinq3

; Registry key to check for directory (so if you install again, it will
; overwrite the old one automatically)
InstallDirRegKey HKLM "Software\kingpinq3" "Install_Dir"

;--------------------------------
;Interface Settings

!define MUI_ABORTWARNING

;--------------------------------
;Pages

#!insertmacro MUI_PAGE_LICENSE "${NSISDIR}\Docs\Modern UI\License.txt"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages

!insertmacro MUI_LANGUAGE "English"

;--------------------------------

; The stuff to install
Section "kingpinq3 (required)"

  SectionIn RO

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  ; Put file there
  File "../../build/release-mingw32-x86/kpq3ded.x86.exe"
  File "../../build/release-mingw32-x86/kingpinq3.exe"
;   File "../../COPYING.txt"
;   File "../../README.txt"
;  File "../../id-readme.txt"
;   File "../../voip-readme.txt"

  SetOutPath "$INSTDIR\basekpq3"
  File "../../build/release-mingw32-x86/basekpq3/cgamex86.dll"
  File "../../build/release-mingw32-x86/basekpq3/qagamex86.dll"
  File "../../build/release-mingw32-x86/basekpq3/uix86.dll"
  SetOutPath "$INSTDIR\missionpack"
;  File "../../build/release-mingw32-x86/missionpack/cgamex86.dll"
;  File "../../build/release-mingw32-x86/missionpack/qagamex86.dll"
;  File "../../build/release-mingw32-x86/missionpack/uix86.dll"

  ; Write the installation path into the registry
  WriteRegStr HKLM SOFTWARE\kingpinq3 "Install_Dir" "$INSTDIR"

  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\kingpinq3" "DisplayName" "kingpinq3"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\kingpinq3" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\kingpinq3" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\kingpinq3" "NoRepair" 1
  WriteUninstaller "uninstall.exe"

SectionEnd

; Optional section (can be disabled by the user)
Section "Start Menu Shortcuts"

  CreateDirectory "$SMPROGRAMS\kingpinq3"
  CreateShortCut "$SMPROGRAMS\kingpinq3\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\kingpinq3\kingpinq3.lnk" "$INSTDIR\kingpinq3.x86.exe" "" "$INSTDIR\kingpinq3.exe" 0

SectionEnd

Section "SDL.dll"

  SetOutPath $INSTDIR

  File "SDL.dll"

SectionEnd

Section "libcurl"

  SetOutPath $INSTDIR

  File "libcurl-4.dll"

SectionEnd

Section "OpenAL-Soft library"

  SetOutPath $INSTDIR

  File "openal32.dll"

SectionEnd

;--------------------------------

; Uninstaller

Section "Uninstall"

  ; Remove registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\kingpinq3"
  DeleteRegKey HKLM SOFTWARE\kingpinq3

  ; Remove files and uninstaller
  Delete $INSTDIR\basekpq3\cgamex86.dll
  Delete $INSTDIR\basekpq3\qagamex86.dll
  Delete $INSTDIR\basekpq3\uix86.dll
;  Delete $INSTDIR\missionpack\cgamex86.dll
;  Delete $INSTDIR\missionpack\qagamex86.dll
;  Delete $INSTDIR\missionpack\uix86.dll
  Delete $INSTDIR\kingpinq3.x86.exe
  Delete $INSTDIR\kpq3ded.x86.exe

  Delete $INSTDIR\COPYING.txt
  Delete $INSTDIR\README.txt
  Delete $INSTDIR\id-readme.txt
  Delete $INSTDIR\voip-readme.txt

  Delete $INSTDIR\SDL.dll

  Delete $INSTDIR\libcurl-4.dll

  Delete $INSTDIR\openal32.dll

  Delete $INSTDIR\uninstall.exe

  ; Remove shortcuts, if any
  Delete "$SMPROGRAMS\kingpinq3\Uninstall.lnk"
  Delete "$SMPROGRAMS\kingpinq3\kingpinq3.lnk"

  ; Remove directories used
  RMDir "$SMPROGRAMS\kingpinq3"
  RMDir "$INSTDIR\basekpq3"
;  RMDir "$INSTDIR\missionpack"
  RMDir "$INSTDIR"

SectionEnd
